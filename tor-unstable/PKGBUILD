# $Id: PKGBUILD 92386 2013-06-04 12:52:29Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: simo <simo@archlinux.org>
# Contributor: Sid Karunaratne

pkgname=tor-unstable
mypkgname=tor
provides=( tor )
conflicts=( tor )
pkgver=0.2.4.17_rc
mypkgver=$(echo ${pkgver} | sed 's/_/-/')
pkgrel=3
pkgdesc='Anonymizing overlay network.'
arch=('i686' 'x86_64' 'armv7h')
url='http://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'torsocks' 'bash')
makedepends=('ca-certificates')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf')
install='tor.install'
source=("http://www.torproject.org/dist/${mypkgname}-${mypkgver}.tar.gz"{,.asc}
        'torrc'
        'tor.service')
md5sums=('2cdfb8dcc3306a43cf465a858bf97b2d'
         'SKIP'
         'c1dd4004c63edaeaa829e01be5413cfc'
         'a396657e6f6df21d319ad586500c04b1')

build() {
  cd "${srcdir}/${mypkgname}-${mypkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${mypkgname}-${mypkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm0700 "${pkgdir}/var/lib/tor"

  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${mypkgname}/LICENSE"

  install -Dm0644 "${srcdir}/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"
}
